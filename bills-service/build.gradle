import config.Androidx
import config.DI
import config.Kotlinx
import config.Testing
import config.Tools

apply plugin: 'com.android.library'
apply from: "$rootDir/build-system/android.gradle"

apply plugin: 'kotlin-kapt'

android {
    defaultConfig {
        // Needed to use Java 8 desugaring with minSdk below 20
        // Maybe I should go up to minSdk 21? ðŸ˜¢ðŸ˜’
        multiDexEnabled true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        // ...
        freeCompilerArgs += [
            "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
        ]
    }
}

dependencies {
    coreLibraryDesugaring(Tools.desugaring)

    api(
        project(":domain"),
        project(":common")
    )

    api(Kotlinx.Coroutines.android)

    implementation(Androidx.Room.core, DI.dagger)
    kapt(Androidx.Room.compiler, DI.compiler)

    testImplementation(
        Androidx.Room.testing,
        Kotlinx.Coroutines.test,
        Testing.robolectric
    )
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}