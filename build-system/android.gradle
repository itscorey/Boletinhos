import config.Androidx
import config.AppConfig
import config.Espresso
import config.Kotlinx
import config.Testing

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: "$rootDir/jacoco.gradle"

android {
    compileSdkVersion AppConfig.Sdk.compile

    defaultConfig {
        minSdkVersion AppConfig.Sdk.min
        targetSdkVersion AppConfig.Sdk.target

        versionCode AppConfig.Version.code
        versionName AppConfig.Version.toString()

        testInstrumentationRunner AppConfig.testRunner
        testInstrumentationRunnerArguments clearPackageData: 'true'

        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true

            all {
                jacoco.includeNoLocationClasses = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    [Androidx.core, Androidx.appCompat, Kotlinx.stbLib].forEach { dependency ->
        implementation dependency
    }

    androidTestImplementation(
        Androidx.Testing.core,
        Androidx.Testing.rules,
        Androidx.Testing.junit,
        Androidx.Testing.runner,
        Espresso.core,
        Testing.assertK
    )

    androidTestUtil(Androidx.Testing.orchestrator)
}